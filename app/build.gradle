plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-android-extensions'
    id 'maven-publish'
}

group = "com.github.lamlong97"

def libraryVersionCode = 101
def libraryVersionName = "1.0.32"

android {
    namespace "com.longlam.documentscanner"
    compileSdkVersion 33  

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 33
        versionCode libraryVersionCode
        versionName libraryVersionName
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs += ["-Xskip-metadata-version-check"]
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/kotlin'
            res.srcDirs = ['src/main/res']
            manifest.srcFile 'src/main/AndroidManifest.xml'
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

configurations.all {
    resolutionStrategy {
        force 'androidx.core:core:1.8.0'
        force 'androidx.core:core-ktx:1.8.0'
        force 'androidx.lifecycle:lifecycle-common:2.5.1'
        force 'androidx.lifecycle:lifecycle-runtime:2.5.1'
        force 'androidx.lifecycle:lifecycle-viewmodel:2.5.1'
        
        eachDependency { details ->
            if (details.requested.group == 'androidx.core') {
                if (details.requested.name == 'core' || details.requested.name == 'core-ktx') {
                    details.useVersion '1.8.0'
                    details.because 'Force androidx.core to 1.8.0 for compileSdk 33 compatibility'
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.7.20"

    // Use api to propagate version constraints to consuming apps
    api 'androidx.core:core:1.8.0'
    api 'androidx.core:core-ktx:1.8.0'
    
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'com.github.mehdico:Tiny-OpenCV:4.12.3-1'

    implementation "androidx.camera:camera-camera2:1.0.2"
    implementation "androidx.camera:camera-lifecycle:1.0.2"
    implementation "androidx.camera:camera-view:1.0.0-alpha28"

    // RxPermissions (được host trên Maven Moxtra)
    implementation 'com.github.tbruyelle:rxpermissions:0.12'

    implementation 'androidx.exifinterface:exifinterface:1.3.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2-native-mt'
    implementation 'id.zelory:compressor:3.0.1'
}

// gradle task: publishToMavenLocal
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.lamlong97'
                artifactId = 'document-scanner'
                version = libraryVersionName
                
                // Add POM customization to enforce dependency versions
                pom.withXml {
                    def dependenciesNode = asNode().dependencies[0]
                    if (dependenciesNode) {
                        // Update existing androidx.core dependencies to version 1.8.0
                        dependenciesNode.dependency.each { dep ->
                            if (dep.groupId.text() == 'androidx.core') {
                                if (dep.artifactId.text() == 'core' || dep.artifactId.text() == 'core-ktx') {
                                    dep.version[0].value = '1.8.0'
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}
